# Configurations
id = Math.floor((Math.random()*1000000000)+1)
uri = "//<%= settings.firehose_host %>/squirrels/#{id}"

# Classes
class Squirrel extends Backbone.Model
  defaults: 
    scale: 1.0

class SquirrelView extends Backbone.View
  initialize: =>
    @model.on('change', @render)

  render: =>
    # Render out the wisdom bubble
    @$el.find('.wisdom').text(@model.get('wisdom'))

    img = @$el.find('img')
    scale = @model.get('scale')

    unless @originalDimensions
      @originalDimensions =
        width: img.width()
        height: img.height()
    # Now scale it
    img.animate
      width:  @originalDimensions.width * scale
      height: @originalDimensions.height * scale

# Demo setup
nutty = new Squirrel
  scale: 1.0
  wisdom: 'A nut a day keeps the doctor away.'

# Connect to the firehose server! Yay!
new Firehose.Consumer
  uri: uri
  message: (json) ->
    nutty.set json
.connect()

$(document).ready ->

$(window).load ->
  # Setup a squirrel view
  view = new SquirrelView
    el: $('#nutty'),
    model: nutty
  # Replace global demo URLs with local ones that peeps can use.
  $('#squirrel_push_demo').html("$ curl 'http:" + uri + "' -X PUT -d '{\"wisdom\": \"Pack away enough nuts for the winter so you can grow big and tall!\", \"scale\": \"1.5\"}'")

  # We have to wait for the squirrel image to load for its dimensions
  view.render()